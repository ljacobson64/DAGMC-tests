  MCNPX test problem 1: R. Prael and J. Wallenius 971120
c ---------------------------------------------------------------
c 256 MeV protons on a cylindrical U238 target of 4 cm radius and
c 5 cm thickness. Source uniformly distributed over circle of
c 0.5 cm radius. Tallies of proton, photon and neutron current.
c ---------------------------------------------------------------
c Cells
c ---------------------------------------------------------------
 11  12 -18.98  -10 20 -30
 21  0          -40 (10:-20:30)
 31  0           40

c ---------------------------------------------------------------
c Surfaces
c ---------------------------------------------------------------
 10  cz  4.0
 20  pz  0.0
 30  pz  5.0
 40  so  50.0

dbcn  26j  1  j  0  12j  1  2j 1
c ---------------------------------------------------------------
c Materials
c ---------------------------------------------------------------
  m12  92238.40c 1      $ original: 92238.60c 1
c ---------------------------------------------------------------
c Source
c ---------------------------------------------------------------
  sdef sur = 20 erg = 256 par = h dir = 1 pos = 0 0 0 rad = d1
  si1  0.5
c ---------------------------------------------------------------
c Options
  tropt  mcscat=gaussian
  imp:n 1 1 0
  imp:h 1 1 0
  phys:n 260
  phys:h 260
  phys:p 4j 1
  mode n h p d t s a
  lca   2 1 6j 1
  lea   2j 0
c ---------------------------------------------------------------
c Tallies
c ---------------------------------------------------------------
  FQ0 E C
  F11:n 40
  FT11  frv 0 0 1
  F21:p 40
  FT21  frv 0 0 1
  C0  -0.9063 -0.8192 -0.5736 -0.4226
       0.4617  0.5373  0.8434  0.8870 1 T
  E11 0.5 20LOG 256
  E21 0.5 20LOG 256
  F14:n 11
  F24:p 11
  F34:h 11
  F44:d 11
  F54:t 11
  F64:s 11
  F74:a 11
  F16:n 11
  F26:p 11
  F36:h 11
  F46:d 11
  F56:t 11
  F66:s 11
  F76:a 11
  print -10 6i -80 -72 -85 -86 -98 -160
  vol 1 0 0
  totnu no
  nps 1000
  prdmp 2j -1 j -1
  ssw   40 sym=2
  histp
